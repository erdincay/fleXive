/***************************************************************
 *  This file is part of the [fleXive](R) framework.
 *
 *  Copyright (c) 1999-2008
 *  UCS - unique computing solutions gmbh (http://www.ucs.at)
 *  All rights reserved
 *
 *  The [fleXive](R) project is free software; you can redistribute
 *  it and/or modify it under the terms of the GNU Lesser General Public
 *  License version 2.1 or higher as published by the Free Software Foundation.
 *
 *  The GNU Lesser General Public License can be found at
 *  http://www.gnu.org/licenses/lgpl.html.
 *  A copy is found in the textfile LGPL.txt and important notices to the
 *  license from the author are found in LICENSE.txt distributed with
 *  these libraries.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  For further information about UCS - unique computing solutions gmbh,
 *  please see the company website: http://www.ucs.at
 *
 *  For further information about [fleXive](R), please see the
 *  project website: http://www.flexive.org
 *
 *
 *  This copyright notice MUST APPEAR in all copies of the file!
 ***************************************************************/

/**
 * Sql Parser Engine.
 *
 * @author Gregor Schober (gregor.schober@flexive.com), UCS - unique computing solutions gmbh (http://www.ucs.at)
 * @version $Rev$
 */
options {
  STATIC = false;
  UNICODE_INPUT=true;  
}


PARSER_BEGIN(SQL)

package com.flexive.sqlParser;
import com.flexive.sqlParser.*;
import java.io.Serializable;
import java.util.ArrayList;

public class SQL {
	boolean insideforcedBrace=false;
}

PARSER_END(SQL)

SKIP : {
	"/*" : WithinComment |
	"-- " : WithinLineComment |
    " " |
	"\t" |
	"\n" |
	"\r"
}

<WithinComment> SKIP : { "*/" : DEFAULT }
<WithinComment> MORE : { <~[]> }

<WithinLineComment> SKIP : { "\n" : DEFAULT }
<WithinLineComment> MORE : { <~[]> }


TOKEN [IGNORE_CASE] :
{
  < BRACE_CI_O  : "{"(["{"," ","\t","\n","\r"])* >
| < BRACE_CI_C  : "}"(["}"," ","\t","\n","\r"])* >
| < BRACE_OPEN  : "(" >
| < BRACE_CLOSE : ")" >
| < GROUP_BY    : "GROUP BY" >
| < AND_OR      : ("AND"|"OR") >
| < ASC_DESC    : ("ASC"|"DESC"|"ASCENDING"|"DESCENDING") >
| < FROM        : "FROM " >
| < WHERE       : "WHERE" >
| < FILTER      : "FILTER" >
| < NOT         : "NOT" >
| < UNION       : "UNION" >
| < EQUALS    : "=" >
| < NEAR    : "~=" >
| < MORE_THAN : ">" >
| < LESS_THAN : "<" >
| < GEQ       : ">=" >
| < LEQ       : "<=" >
| < NOT_GTR   : "!>" >
| < IS_CHILD_OF   : "IS"(" ")+"CHILD"(" ")+"OF" >
| < IS_DIRECT_CHILD_OF   : "IS"(" ")+"DIRECT"(" ")+"CHILD"(" ")+"OF" >
| < IN_BRIEFCASE : "EXISTS"(" ")+"IN"(" ")+"BRIEFCASE" >
| < NOT_LSS   : "!<" >
| < NEQ       : "!=" >
| < NEQ2      : "<>" >
| < IS        : "IS" >
| < IS_NOT    : "IS NOT" >
| < NULL      : "NULL" >
| < LIKE      : "LIKE" >
| < SUBSTRING : "SUBSTRING" >
| < CONTAINS : "CONTAINS" >
| < UPPER     : "UPPER" >
| < LOWER     : "LOWER" >
| < ORDER_BY  : "ORDER"(" ")*"BY" >
| < IN_OR_NOTIN: ("IN"|"NOT IN") >
| < SELECT    : "SELECT " >
| < AS        : "AS" >
| < DISTINCT    : "DISTINCT " >
| < RELATION    : "RELATION" >
| < RECURSIVE    : "RECURSIVE" >
| < END_STMT  : ";"([" ","\n"])* >
| < FUNCTION  : (["a"-"z","A"-"Z","_"])+"(">
| < WILDCARD  : "*" >
| < USERWILDCARD : "@*" >
| < INTEGER   : ("-"|"")["0"-"9"] (["0"-"9"])* >
| < FLOAT     : ("-"|"")(["0"-"9"])*(".")(["0"-"9"])+>
| < FXPK      : (["0"-"9"])+(".")("MAX"|"LIVE") >
| < STRING:    ("'"(~["'"]|"''")*"'") >
| < BOOLEAN   : ("TRUE" | "FALSE") >
| < LIST_SEPERATOR : "," >
| < IDENTIFIER: ["@","a"-"z","A"-"Z","_"] ( ["!","a"-"z","A"-"Z","_","0"-"9"] )* >
| < SYM_SELECT_ITEM: (  // Table alias + property name, e.g. co.propname or propname.field or "co.propname"
                        ( ["a"-"z","A"-"Z","_","0"-"9"] )* (["."])?       // table alias
                        ((["*","@","#","a"-"z","A"-"Z","_"]               // name
                         ( [".","a"-"z","A"-"Z","_","0"-"9"] )*) )

                        |   // quoted property XPath, e.g. #article/caption
                        ("#"
                            ["a"-"z","A"-"Z","_"]
                            (["a"-"z","A"-"Z","_","0"-"9","/","."])* 
                        )
                     ) >
}



/**
 * Entry point of the parser.
 */
FxStatement statement() throws SqlParserException:
{
	FxStatement PI = new FxStatement();
}{	
	select(PI) [<END_STMT>] <EOF>
	{
		PI.cleanup();
		return PI;
	}
}


/**
 *  Select expression.
 */
FxStatement select(final FxStatement PI) throws SqlParserException :  {
	String grp;
	String finalQuery = null;
}{
	<SELECT>
	[<DISTINCT>
	{
	    PI.setDistinct(true);
	}
	]
	selectedValues(PI) [ from_clause(PI) ] [ <FILTER> filterList(PI) ]
	[ <WHERE> where(PI) ]
	grp=group_by_clause(PI)
	[ order_by(PI) ]
	[<UNION>
	{
		throw new SqlParserException("ex.sqlSearch.unionNotSupported");
	}
	select(PI)]  
	{
		return PI;
	}
}


/**
 * Order by.
 * eg. ORDER BY ct.COL1,ct.COL2 ASC
 */
void order_by(final FxStatement PI) throws SqlParserException :{
}{
	<ORDER_BY> symItemListOrder(PI)
}


/**
 * Group by.
 * eg. GROUP BY ct.COL1,ct.COL2
 */
String group_by_clause(final FxStatement PI) : {
	String grp="";
}{
	<GROUP_BY> grp=symItemList(PI)
	{ 
		return "GROUP BY "+grp;
	} | { 
		return ""; 
	}
}

/**
 * Sym item list order.
 * For example "<alias.colum> ASC, <alias.colum> DESC, ..."
 */
void symItemListOrder(final FxStatement PI) throws SqlParserException :
{
	Token tk;
	Token ascDesc = null;
}
{
	(tk=<SYM_SELECT_ITEM> | tk=<INTEGER> | tk=<IDENTIFIER>) [ascDesc=<ASC_DESC>] [ <LIST_SEPERATOR> symItemListOrder(PI) ]
	{
		String _sAsc = (ascDesc==null)?null:ascDesc.image.toUpperCase();
		final boolean asc = (_sAsc==null || _sAsc.indexOf("ASC")!=-1)?true:false;
		PI.addOrderByValue(new OrderByValue(tk.image,asc));
	}
}


/**
 * Sym item list.
 * For example "<alias.column>, <alias.column>, ..."
 */
String symItemList(final FxStatement PI) :
{
  Token tk;
  Token sep=null;
  String list="";
}
{
	tk=<SYM_SELECT_ITEM> [ sep=<LIST_SEPERATOR> list=symItemList(PI) ]
	{ 
		return tk.image+((sep!=null)?sep.image:"")+list;
	}
	|
	tk=<INTEGER> [ sep=<LIST_SEPERATOR> list=symItemList(PI) ]
	{
		return tk.image+((sep!=null)?sep.image:"")+list;
	}
}



/**
 * Select list.
 * <value>, <value>, ...
 */
void selectedValues(final FxStatement PI) throws SqlParserException :
{
    Token t1;
    Value vi;
}
{
	(t1 = <WILDCARD> | t1 = <USERWILDCARD>)
	{
	    final Property p = new Property("co", t1.image, null);
		PI.addSelectedValue(p, null);
	}
	|
	vi=selectedValue(PI) [ <LIST_SEPERATOR> selectedValues(PI) ]
}


/**
 * Value may be a property, number, or string with our without enclosing functions
 */
Value selectedValue(final FxStatement PI)  throws SqlParserException  :
{ 
	Value vi;
	Token t1; 
	Token alias=null;
	Constant con;
}{
	t1=<FUNCTION> vi=selectedValue(PI) <BRACE_CLOSE>
	{
		return vi.addFunction(t1.image);
	}
	| t1=<SYM_SELECT_ITEM> [[<AS>] alias=<IDENTIFIER>]
	{
		Property p = new Property(PI, t1.image);
		PI.addSelectedValue(p,alias==null?null:alias.image);
		return p;
	}
	| t1=<IDENTIFIER> [[<AS>] alias=<IDENTIFIER>]
	{
	    // a property without table alias
        Property p2 = new Property("co",t1.image,null);
        PI.addSelectedValue(p2,alias != null ? alias.image : t1.image);
        return p2;
	}
	| con=constantValue() [[<AS>] alias=<IDENTIFIER>]
	{
		PI.addSelectedValue(con,alias==null?null:alias.image);
		return con;
	}
}

/**
 * List of all tables to select from.
 * Every table must be specified with an alias.
 */
void from_clause(final FxStatement PI) throws SqlParserException : {
}{
	<FROM> table_list(PI)
}


/**
 * List of tables.
 * Every table must specify an alias. The list is comma seperated
 */
void table_list(final FxStatement PI) throws SqlParserException : {
	String list="";
	Token table;
	Token alias=null;
	Token sep=null;
}{
	table=<IDENTIFIER> [  LOOKAHEAD(2) alias=<IDENTIFIER> ] [ sep=<LIST_SEPERATOR> table_list(PI) ]
	{
		PI.addTable(new Table(table.image,alias.image));		
	}
}


/**
 * FleXive FILTER clause.
 */
void filterList(final FxStatement PI) throws SqlParserException :{
	Token itemId; 
	String itemValue;
}{
	LOOKAHEAD(3)
	itemId=<SYM_SELECT_ITEM> <EQUALS> itemValue=filterItem() [ <LIST_SEPERATOR> filterList(PI) ]
	{
		new Filter(PI,itemId.image,itemValue);
	}	
	|
	itemId=<IDENTIFIER> <EQUALS> itemValue=filterItem() [ <LIST_SEPERATOR> filterList(PI) ]	
	{
		new Filter(PI,itemId.image,itemValue);
	}	
}

/**
 * Item within the filter
 */
String filterItem() :{
	Token item;
	String val=null;
}{
	item = <STRING> ["|" val=filterItem()]
	{
		// Discard starting and ending "'"
		return item.image.substring(1,item.image.length()-1)+((val!=null)?"|"+val:"");
	}
	|
	item = <IDENTIFIER> ["|" val=filterItem()]
	{return item.image+((val!=null)?"|"+val:"");}
	|
	item = <INTEGER> ["|" val=filterItem()]
	{return item.image+((val!=null)?"|"+val:"");}
	|
	item = <BOOLEAN> ["|" val=filterItem()]
	{return item.image+((val!=null)?"|"+val:"");}
}




/**
 * Where clause.
 */
void where(final FxStatement PI) throws SqlParserException : {
  String searchc;
  Token braceC= null;
  Token braceO= null;
  Token comOrInt = null;
  FxStatement combine = null;
}{
    [ braceO=<BRACE_CI_O> ] search_condition(PI) [ braceC=<BRACE_CI_C> ] {
        //PI.setStartingBraces(braceO);
        //PI.setSearchConditions(searchc);
        //PI.setEndingBraces(braceC);
    }
}


/**
 * Search condition
 */
void search_condition(final FxStatement PI) throws SqlParserException: {
  String pred=null;
  String sc="";
  Token andOr=null;
  String ret = null;
  String sAndOr  = null;
  Token braceO1 = null;
  Token braceO2 = null;
  Token braceC = null;
	Condition cond;
}{
	/* ( search_condition ) [ and search_condition ] */
	LOOKAHEAD(2) <BRACE_OPEN> 
	{
		PI.startSubBrace();
	}
	search_condition(PI) 
	<BRACE_CLOSE>
	{
		PI.endSubBrace();
	} 
	[
		andOr=<AND_OR>
		{
			Brace br = PI.getCurrentBrace();
			if (insideforcedBrace) {
				Brace parent = br.getParent();
				if ( parent.getType()==null || parent.getType().equals(andOr.image)) {
				    parent.setType(andOr.image);
					insideforcedBrace=false;
					PI.endSubBrace();
				} else {
				    //
				}

			} else {
			    insideforcedBrace=br.processAndOr(andOr.image,insideforcedBrace);
			}
			PI.getCurrentBrace().setType(andOr.image);
		}
		search_condition(PI) 
	]

	|

	cond =condition(PI)  
	{
	    Brace br = PI.getCurrentBrace();
		br.addElement(cond);
		//System.out.println(cond+": type="+br.getType()+" fi="+insideforcedBrace);
	}
	[
		andOr=<AND_OR> 
		{
		    insideforcedBrace=br.processAndOr(andOr.image,insideforcedBrace);
		}
		search_condition(PI) 
	]

}




/**
 * Eg. a=b,ct.ID=1,fct(a=b),fct(fct(a))=b,...
 */
Condition condition(final FxStatement PI) throws SqlParserException: {
	ConstantArray arr = null;
	Value vi1 = null;
	Value vi2 = null;
	Condition.ValueComparator fct = null;
	Token inNotIn = null;
	Token symSel = null;
	Token nullToken = null;
	Token varFctName= null;
	String params = null;	
	String relationNames = null;
	String relationFroms = null;
}{
	LOOKAHEAD(3)
	vi1=value(PI) fct=comperator(PI) vi2=valueOrSubselect(PI)
	{
		return new Condition(PI,vi1,fct,vi2);
	}
        |
        <IS_CHILD_OF>  vi1=valueOrSubselect(PI) {
		return new Condition(PI,vi1,Condition.ValueComparator.IS_CHILD_OF,null);
        }
        |
        <IS_DIRECT_CHILD_OF>  vi1=valueOrSubselect(PI) {
		return new Condition(PI,vi1,Condition.ValueComparator.IS_DIRECT_CHILD_OF,null);
        }
	|
	vi1=value(PI) inNotIn=<IN_OR_NOTIN> <BRACE_OPEN> arr=constantArray() <BRACE_CLOSE>
	{
		return new Condition(PI,vi1,
		inNotIn.image.equalsIgnoreCase("IN")?Condition.ValueComparator.IN:Condition.ValueComparator.NOT_IN,
		arr);
	}
}



Condition.ValueComparator comperator(final FxStatement PI) throws SqlParserException:
{ String id;String id2=null;Token sep=null; }
{
  <LIKE>
  { return Condition.ValueComparator.LIKE; }
  | <NOT> <LIKE>
  { return Condition.ValueComparator.NOT_LIKE; }
  | <EQUALS>
  { return Condition.ValueComparator.EQUAL; }
  | <MORE_THAN>
  { return Condition.ValueComparator.GREATER; }
  | <LESS_THAN>
  { return Condition.ValueComparator.LESS; }
  | <GEQ>
  { return Condition.ValueComparator.GREATER_OR_EQUAL; }
  | <LEQ>
  { return Condition.ValueComparator.LESS_OR_EQUAL; }
  | <NOT_GTR>
  { return Condition.ValueComparator.NOT_GREATER; }
  | <NOT_LSS>
  { return Condition.ValueComparator.NOT_LESS; }
  | <NEQ>
  { return Condition.ValueComparator.NOT_EQUAL; }
  | <NEQ2>
  { return Condition.ValueComparator.NOT_EQUAL; }
  | <NEAR>
  { return Condition.ValueComparator.NEAR;}
  | <IS>
  { return Condition.ValueComparator.IS;}
  | <IS_NOT>
  { return Condition.ValueComparator.IS_NOT;}
  | <IN_BRIEFCASE>
  { return Condition.ValueComparator.IN_BRIEFCASE;}
}



Value valueOrSubselect(final FxStatement PI) throws SqlParserException :
{ 
	Value vi;
}{
	vi=value (PI)
	{ 
		return vi; 
	}
	/*
	| 
	<BRACE_OPEN> select(PI.newSubParsedInfo())<BRACE_CLOSE>
	{ 
		throw new SqlParserException("ex.sqlSearch.subselectsNotSupported");
		//return new Value("("+val+")",true); 
	}
	*/
}

/**
 * Value may be a property, number, or string with our without enclosing functions
 */
Value value(final FxStatement PI)  throws SqlParserException  :
{ 
	Value vi;
	Token t1,nr1,nr2; 
	String sel;
	Constant con;
}{
	t1=<FUNCTION> vi=value(PI) <BRACE_CLOSE>
	{
		return vi.addFunction(t1.image);
	}
	| <SUBSTRING> <BRACE_OPEN> vi=value(PI) <LIST_SEPERATOR> nr1=<INTEGER> <LIST_SEPERATOR> nr2=<INTEGER> <BRACE_CLOSE>
	{
		throw new SqlParserException("ex.sqlSearch.substringNotSupported");
	}
	| t1=<IDENTIFIER>
	{
		// A property with NO table alias is referenced
		return new Property("co", t1.image, null);
	}
	| <WILDCARD>
	{
	    // fulltext query
	    return new Property("co", "*", null);
	}
	| t1=<SYM_SELECT_ITEM> 
	{
		return new Property(PI, t1.image);
	}
	| LOOKAHEAD(3) <BRACE_OPEN> con=constantArray() <BRACE_CLOSE> {
	    return con;
	}
	| LOOKAHEAD(3) con=constantValue()
	{
		return con;
	}
}

/**
 * Constant value: number string or null
 */
Constant constantValue() :
{ Token t1=null;}
{
    <BRACE_OPEN> constantValue() <BRACE_CLOSE>
    |
	t1=<STRING>
	{	
		// Remove quotes: return new Constant(t1.image.substring(1,t1.image.length()-1));
		return new Constant(t1.image);
	}
	| 
	t1=<INTEGER>
	{
		return new Constant(t1.image);
	}
	|
	t1=<FLOAT>
	{
		return new Constant(t1.image);
	}
	|
	t1=<BOOLEAN>
	{
	    return new Constant(t1.image);
	}
	|
	t1=<FXPK>
	{
	    return new Constant(t1.image);
	}
	|
	<NULL> 
	{
		return new Constant(null);
	}

}


/**
 * Returns a value list as array
 */
ConstantArray constantArray() throws SqlParserException :
{
	Constant con;
	ConstantArray arr = null;
}{
	con=constantValue() [ <LIST_SEPERATOR> arr=constantArray() ]
	{
		if (arr!=null) {
			return arr.add(con);
		} else {
			return new ConstantArray(new Constant[]{con});
		}
	}
}


