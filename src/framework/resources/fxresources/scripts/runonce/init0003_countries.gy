/***************************************************************
 *  This file is part of the [fleXive](R) project.
 *
 *  Copyright (c) 1999-2008
 *  UCS - unique computing solutions gmbh (http://www.ucs.at)
 *  All rights reserved
 *
 *  The [fleXive](R) project is free software; you can redistribute
 *  it and/or modify it under the terms of the GNU General Public
 *  License as published by the Free Software Foundation;
 *  either version 2 of the License, or (at your option) any
 *  later version.
 *
 *  The GNU General Public License can be found at
 *  http://www.gnu.org/copyleft/gpl.html.
 *  A copy is found in the textfile GPL.txt and important notices to the
 *  license from the author are found in LICENSE.txt distributed with
 *  these libraries.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  For further information about UCS - unique computing solutions gmbh,
 *  please see the company website: http://www.ucs.at
 *
 *  For further information about [fleXive](R), please see the
 *  project website: http://www.flexive.org
 *
 *
 *  This copyright notice MUST APPEAR in all copies of the file!
 ***************************************************************/


/**
 * Initializes the COUNTRIES select list. Based on the Unicode territories retrieved
 * from http://www.unicode.org/cldr/repository_access.html.
 * Remove the DTD at the top of the XML files to avoid DTD lookups by the XML parser.
 *
 * @author Daniel Lichtenberger (daniel.lichtenberger@flexive.com), UCS - unique computing solutions gmbh (http://www.ucs.at)
 * @version $Rev$
 */

import com.flexive.shared.*
import com.flexive.shared.structure.*
import com.flexive.shared.security.ACL
import com.flexive.shared.value.FxString

def cl = Thread.currentThread().getContextClassLoader()
def final environment = CacheAdmin.getEnvironment()
def final ACL itemAcl = environment.getACL(ACL.Category.SELECTLISTITEM.getDefaultId())
final translations = [:]

["de", "en"].each { languageIso ->
    final FxLanguage language = EJBLookup.languageEngine.load(languageIso)
    final xml = com.flexive.shared.FxSharedUtils.loadFromInputStream(cl.getResourceAsStream("fxresources/scripts/runonce/countries/${languageIso}.xml"), -1)
    final countries = new XmlSlurper().parseText(xml)   // don't validate to avoid errors when unicode.org is offline
    final rows = countries.localeDisplayNames.territories.territory.list()
    rows.each {
        final String cc = it."@type".toString().toLowerCase()    // get country code
        if (org.apache.commons.lang.StringUtils.isAlpha(cc)) {  // skip world regions and continents (europe, north america, ...)
            if (translations[cc] == null) {     // initialize
                translations[cc] = new FxString(true, cc)
            }
            translations[cc].setTranslation(language, it.toString())    // store translation
        }
    }
}

FxSelectListEdit selectList = FxSelectListEdit.createNew("COUNTRIES",
        new FxString(true, FxLanguage.ENGLISH, "Countries").setTranslation(FxLanguage.GERMAN, "L\u00e4nder"),
        new FxString(true, FxLanguage.ENGLISH, "Countries").setTranslation(FxLanguage.GERMAN, "L\u00e4nder"),
        true, itemAcl, itemAcl)
def createItem = { trans ->
    new FxSelectListItemEdit(trans.key.toUpperCase(), itemAcl, selectList, trans.value, trans.key, "#000000")
}
// put preferred countries first
/*["de", "at", "ch"].each { cc ->
    createItem([key: cc, value: translations.remove(cc)])
} */
// now store other countries
translations.each(createItem)

EJBLookup.selectListEngine.save(selectList)

